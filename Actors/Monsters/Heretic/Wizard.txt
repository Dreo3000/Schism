//=============================================================================================
//
// Disciple/Wizard (Some code from Bitter Heretic and Some from Cacodemon/etc. from Schism)
// modified by LordADHD
//=============================================================================================

// All of the changes here (08.02.25) is temporary

Actor Schism_HereticWizard replaces Wizard {
	Game Raven
	BloodType "Brutal_Blood", "SawBlood", "SawBlood"
    GibHealth 25
    damagefactor "Crush", 10.0
	damagefactor "SSG", 100.0
	Radius 16
	Height 68
	Mass 400
	Speed 12
	Health 180
	PainChance 128
	PainChance "Kick", 255
	PainChance "Avoid", 90
	PainChance "Saw", 255
	PainChance "Stun", 125
	PainChance "Freeze", 255
	damagefactor "ExplosiveImpact", 3.0 damagefactor "Shrapnel", 3.0
	damagefactor "GibRemoving", 0.0 damagefactor "HangingHook", 0
	Monster
	Scale 0.9
	+FLOAT
	+NOGRAVITY 
	+AVOIDMELEE
	SeeSound "wizard/sight"
	PainSound "wizard/pain"
	DeathSound "wizard/death"
	ActiveSound "wizard/active"
	AttackSound "wizard/attack"
	damagefactor "TeleportRemover", 0.0
	DamageFactor "CauseSplash", 0.0 DamageFactor "CauseObjectsToSplashSlime", 0.0 DamageFactor "CauseObjectsToSplashNukage", 0.0
	DamageFactor "CauseObjectsToSplashBlood", 0.0 DamageFactor "CauseObjectsToSplashLava", 0.0
	Obituary "$OB_WIZARD"
	HitObituary "$OB_WIZARDHIT"
	DeathHeight 4
	BurnHeight 0
	Damagefactor "Blood", 0.0 damagefactor "BlueBlood", 0.0 damagefactor "GreenBlood", 0.0
	Damagefactor "Lightning", 0.2
	Tag "Disciple"
	States {

		Spawn:
			WZRD A 0
			WZRD A 1 A_Look
			Goto Stand
			Stand:
			WZRD A 10 A_Look
			Loop

		See:
			WZRD AA 5 A_Chase
			WZRD BB 5 A_Chase("","")
			TNT1 A 0 A_JumpIfCloser(99, "Melee")
			Loop
			
		Melee:
			WZRD O 4 A_Facetarget
			WZRD N 4 A_Recoil(-5)
			WZRD O 4 A_Facetarget
			TNT1 A 0 A_CustomMissile("WizardSlash", 34, 0, 0, 0)
			WZRD P 4 A_Facetarget
			WZRM O 4 A_Facetarget
			WZRM N 4 A_Recoil(-5)
			WZRM O 4 A_Facetarget
			TNT1 A 0 A_CustomMissile("WizardSlash", 34, 0, 0, 0)
			WZRM P 4 A_Facetarget
			Goto See

		Missile:
			TNT1 A 0 A_TakeInventory("EnemyMemory", 30)
			TNT1 A 0 A_JumpIfCloser(140, "Melee")
			TNT1 A 0 A_Jump (198, 3)
			Goto Avoid
			TNT1 AAA 0
			WZRD C 4 BRIGHT A_FaceTarget
			WZRD C 4 BRIGHT A_FaceTarget
			WZRD C 4 BRIGHT A_FaceTarget
			WZRD C 4 BRIGHT A_FaceTarget
			WZRD C 4 BRIGHT A_FaceTarget
			WZRD C 4 BRIGHT A_FaceTarget
			WZRD C 4 BRIGHT A_FaceTarget
			WZRD C 4 BRIGHT A_FaceTarget
			TNT1 A 0 A_CustomMissile("WizardBall", 34, 0, -6, 0)
			TNT1 A 0 A_CustomMissile("WizardBall", 34, 0, 6, 0)
			WZRD D 12 BRIGHT A_CustomMissile("WizardBall", 34, 0, 0, 0)
			Goto See

		Pain:
			WZRD E 3
			WZRD E 3 A_Pain
			WZRD E 6
			Goto See
			
		Pain.Avoid:
			TNT1 A 0
			TNT1 A 0 A_Jump(255, "AvoidLeft", "AvoidRight")
			Goto AvoidLeft

		AvoidLeft:
			WZRD A 1 A_FaceTarget
			TNT1 A 0 ThrustThing(angle*256/360+192, 15, 0, 0)
			WZRD A 5 A_FaceTarget
			Goto Missile
		AvoidRight:
			WZRD A 1 A_FaceTarget
			TNT1 A 0 ThrustThing(angle*256/360+64, 15, 0, 0)
			WZRD A 5 A_FaceTarget
			Goto Missile

		Pain.Kick:
		Pain.HeavyImpact:
			TNT1 A 0 A_FaceTarget
			WZRD E 6 A_Recoil(5)
			WZRD E 6 A_Pain
			WZRD E 10
			Goto See

		Pain.Lightning:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx ("StunElectrocute", random (-4, 4), random (-4, 4),  random (16, 32), 0, 0)
			WZRD E 3 A_Pain
			WZRD E 3 A_SpawnItemEx ("StunElectrocute", random (-12, 12), random (-12, 12),  random (16, 52), 0, 0)
			Goto See

		Pain.Stun:
			TNT1 A 0
			TNT1 A 0 A_SpawnItemEx ("StunElectrocute", random (-4, 4), random (-4, 4),  random (16, 32), 0, 0)
			WZRD E 3 A_Pain
			WZRD EEEE 2 A_SpawnItemEx ("StunElectrocute", random (-12, 12), random (-12, 12),  random (16, 52), 0, 0)
			WZRD E 3 A_Pain
			WZRD EEEEEE 2 A_SpawnItemEx ("StunElectrocute", random (-12, 12), random (-12, 12),  random (16, 52), 0, 0)
			WZRD E 3 A_Pain
			Goto See

		Pain.Cut:
		Pain.Saw:
			TNT1 A 0 A_Stop
			WZRD EE 6
			Goto See


		Avoid:
			NULL A 0 A_FaceTarget
			WZRD A 6 A_FastChase
			NULL A 0 A_FaceTarget
			WZRD A 6 A_FastChase
			NULL A 0 A_FaceTarget
			WZRD B 6 A_FastChase
			NULL A 0 A_FaceTarget
			WZRD B 6 A_FastChase

			WZRD B 5 A_FaceTarget
			WZRD C 5 A_FaceTarget
			TNT1 A 0 A_CustomMissile("WizardBall", 34, 0, -6, 0)
			TNT1 A 0 A_CustomMissile("WizardBall", 34, 0, 6, 0)
			WZRD D 5 BRIGHT A_CustomMissile("WizardBall", 34, 0, 0, 0)
			Goto See

		Death.Dagger:
			TNT1 A 0 A_GiveToTarget("RuneAmmo",2)
		Death:
			WZRD F 6 BRIGHT
			WZRD G 6 BRIGHT
			WZRD H 6 BRIGHT A_Scream

			TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("XDeathWizardBlack", 32, 0, random (0, 360), 2, random (30, 90))
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("XDeathWizardPurple", 32, 0, random (0, 360), 2, random (30, 90))

			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BluePlasmaParticleFast", 30, 0, random (0, 360), 2, random (0, 360))
			EXPL AAAAAAAAAAAAAAAA 0 A_CustomMissile ("PurpleFireBallExplosion", 6, 0, random (0, 360), 2, random (0, 360))

			TNT1 A 0
			TNT1 A -1
			Stop

		Death.Cut:
		Death.Saw:
		Death.SSG:
		Death.Minigun:
				TNT1 A 0
				TNT1 A 0 A_Jump(64, "Death")
				WZRD E 1 A_Scream
				TNT1 A 0 A_NoBlocking
				TNT1 A 0 A_CustomMissile ("RipWizard", 0, 0, random (0, 360), 2, random (0, 160))
				TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile ("XDeathWizardBlack", 32, 0, random (0, 360), 2, random (30, 90))
				TNT1 AAAAAAAAAAAAAAAA 0 A_CustomMissile ("XDeathWizardPurple", 32, 0, random (0, 360), 2, random (30, 90))

				TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BluePlasmaParticleFast", 30, 0, random (0, 360), 2, random (0, 360))
				EXPL AAAAAA 0 A_CustomMissile ("PurpleFireBallExplosionMedium", 6, 0, random (0, 360), 2, random (0, 360))

				TNT1 A 0
				TNT1 A -1
				Stop

		Death.Blast:
		Death.Railgun:
			TNT1 A 0
			TNT1 A 0 A_FaceTarget
			TNT1 A 0 A_Jump(32, "Death.Cut")
			TNT1 A 0 A_Scream
			TNT1 A 0 A_NoBlocking
			TNT1 AAAA 0 A_CustomMissile ("XDeathWizardBlack", 32, 0, random (0, 360), 2, random (30, 90))
			TNT1 AAAAAAAA 0 A_CustomMissile ("XDeathWizardPurple", 32, 0, random (0, 360), 2, random (30, 90))

			TNT1 AAAAAAAA 0 A_CustomMissile ("BluePlasmaParticleFast", 30, 0, random (0, 360), 2, random (0, 360))
			WZRR AAAAAAAA 3 A_CustomMissile ("PurpleFlareWizard", 40, 0, random (0, 360), 2, random (30, 60))
			WZRD F 6 BRIGHT
			WZRD G 6 BRIGHT
			WZRD H 6 BRIGHT

			TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("XDeathWizardBlack", 32, 0, random (0, 360), 2, random (30, 90))
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("XDeathWizardPurple", 32, 0, random (0, 360), 2, random (30, 90))

			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BluePlasmaParticleFast", 30, 0, random (0, 360), 2, random (0, 360))
			EXPL AAAAAAAAAAAAAAAA 0 A_CustomMissile ("PurpleFireBallExplosion", 6, 0, random (0, 360), 2, random (0, 360))

			TNT1 A 0
			TNT1 A -1
			Stop

		Death.Ice:
		Death.Freeze:
		Death.Frost:
			TNT1 A 0
			TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
			TNT1 A 0 A_ChangeFlag("SOLID", 0)
			TNT1 A 0 A_SpawnItemEx("FrozenWizard", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPOINTERS, 0, tid)
			Stop

		Crush:
			TNT1 A 0
			TNT1 AAAA 0 A_SpawnItemEx ("BodyRemovalThing",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
			TNT1 A 1 A_XScream
			Stop


		Pain.KillMe:
		Pain.Taunt:
			TNT1 A 0 HealThing(1)
			Goto Missile
			
		Death.KillMe:
		Death.Taunt:
			TNT1 A 0 A_ChangeFlag("SOLID", 0)
			TNT1 A 0 A_SpawnItem("BrutalWizard")
			Stop
	}
}

//===========================================================================
//
// Wizard "plasma" ball & Wizard "Slash" attack
//
//===========================================================================
ACTOR WizardBall
{
	Game Doom
	Radius 6
	Height 6
	Speed 18
	FastSpeed 24
	Damage (random(20,25))
	Projectile
	+RANDOMIZE
	+THRUGHOST
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 0.95
    damagetype Plasma
	SeeSound "caco/attack"
	DeathSound "caco/shotx"
	Decal "Scorch"
	Scale 0.6
	States
	{
	Spawn:
		FX11 AB 2 BRIGHT
		Loop
	Death:
        TNT1 A 0 A_CustomMissile ("ShoqueAzul", 3, 0, random (0, 360), 2, random (0, 360))
		EXPL A 0 A_CustomMissile ("FireBallExplosionFlames", 1, 0, random (0, 360), 2, random (0, 360))
		EXPL A 0 A_CustomMissile ("BluePlasmaFire", 1, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_CustomMissile ("BDExplosionparticlesSmall", 10, 0, random (0, 180), 2, random (40, 90))
		TNT1 AAA 1 A_SpawnItem("RedFlare")
		Stop
	}
}

ACTOR WizardSlash: ImpAttack
{
    +THRUGHOST
	DamageType HKFT
    Damage 5
}

//===========================================================================
//
// Gibs
//
//===========================================================================

ACTOR XDeathWizardBlack: XDeath1
{
    Speed 8
	Gravity 0.4
	SeeSound "misc/xdeath5"
    DeathSound "misc/xdeath5"
    Scale 1.1
    States
    {
    Spawn:
	    TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
        WZRG EFGH 6
        Loop
    Death:
		TNT1 A 0
		TNT1 A 0 A_QueueCorpse
	    TNT1 A 0 A_CheckFloor("SpawnFloor")
        WZRG E 9009
		TNT1 A 2
        Stop
	SpawnFloor:
	    WZRG E 9009
		TNT1 A 2
		Stop
    }
}

ACTOR XDeathWizardPurple: XDeathWizardBlack
{
    Speed 8
	//Gravity 0.4
    Scale 1.1
	Alpha 0.6
	RenderStyle "Translucent"
    -NOGRAVITY
	States
    {
    /*
	Spawn:
	    TNT1 A 0 A_JumpIf(waterlevel > 1, "Death")
        WZRG ABCD 3
        Loop
    Death:
        WZRG B 100
		WZRG BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 1 A_FadeOut(0.01)
		Stop
	*/
	Spawn:
		TNT1 A 0
        WZRG ABCDABCDABCDABCD 3 BRIGHT A_FadeOut(0.10)
		Stop

    }
}

//===========================================================================
//
// Frozen Stuff
//
//===========================================================================

ACTOR FrozenWizard : FrozenZombieman
{
	States
	{
	Spawn:
        TNT1 A 0 NoDelay A_PlaySound("Cryomancy/EnemyFreeze", 1)
		FRDW A 400
		FRDW A 2
		TNT1 A 0 A_PlaySound("ICEBRK1")
		FRDW A 50
		FRDW B 2
		FRDW A 50
		FRDW B 2
		FRDW A 50
		FRDW B 2
		FRDW A 25
		FRDW B 2
		FRDW A 25
		FRDW B 2
		FRDW A 10
		FRDW B 2
		FRDW A 10
		FRDW B 2
		FRDW A 10
		FRDW BABABA 2
		Goto Death.Burn
		Death.Fire:
		Death.Flames:
		Death.Burn:
		TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
	    TNT1 A 0 A_ChangeFlag("SOLID", 0)
	    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_PlaySound("Cryomancy/EnemyUnfreeze", 1)
		TNT1 AAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("XIceChunk1", 48, 0, random (0, 360), 2, random (0, 160))
	    TNT1 AAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("XIceChunk2", 48, 0, random (0, 360), 2, random (0, 160))
	    TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("XIceChunk3", 48, 0, random (0, 360), 2, random (40, 120))
	    TNT1 A 0 A_NoBlocking
	    TNT1 A 0 A_spawnItem("WizardDamaged")
	    Stop
	Death:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_PlaySound("Cryomancy/EnemyShatter", 1)
		TNT1 AAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("XIceChunk1", 48, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("XIceChunk2", 48, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("XIceChunk3", 48, 0, random (0, 360), 2, random (40, 120))
		Stop
	Death.Freeze:
	Death.Ice:
	Death.Frost:
		TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
		TNT1 A 0 A_ChangeFlag("SOLID", 0)
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItem("FrozenWizard")
		Stop
	}
}

ACTOR WizardDamaged : Schism_HereticWizard
{
	Health 20
	Speed 5
	PainChance "Freeze", 255
	States
	{
	Pain.Freeze:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("IsSlowed", 1, "Pain")
		TNT1 A 0 ACS_NamedExecute("SlowingFreeze",0)
		TNT1 A 0 A_GiveInventory("IsSlowed", 1)
		Goto Super::Pain

	}
}

//===========================================================================
//
// Explosion Stuff
//
//===========================================================================

ACTOR PurpleFireBallExplosion: FlameTrails
{
Scale 1.6
	States
	{
    Spawn:
        //FRFX KKKLLLMMMNNN 3 BRIGHT A_SpawnItem("RedFlareMedium",0,0)
        //FRFX OOOPP 3 BRIGHT

        WZRX ABCDEFGHIJKL 1 BRIGHT A_SpawnItem("PurpleFlareWizard",0,0)
        WZRX MNOPQ 1
        Stop
	}
}

Actor PurpleFireBallExplosionMedium: PurpleFireBallExplosion
{
Scale 0.8
}

ACTOR PurpleFlareWizard: BlueFlareSmall //"PurpleFlareSmall" is missing now, "BlueFlareSmall" is the replacement
{
Scale 0.4
}

Actor BluePlasmaParticleFast: BluePlasmaParticle
{
  Gravity 0.5
  Scale 0.04
  Speed 16
}

//===========================================================================
//
// Half-Rip Stuff
//
//===========================================================================

ACTOR RipWizard
{
	Radius 1
	Height 1
	Scale 0.8
	Speed 0
	Mass 1
	Decal BloodSuper
	BounceFactor 0.5
	//+DOOMBOUNCE
	+MISSILE
	+NOBLOCKMAP
	+NOTELEPORT
	+MOVEWITHSECTOR
	+NOGRAVITY
	+NOCLIP
	+DONTSPLASH
	States
	{
		Spawn:
			TNT1 AAA 0 A_CustomMissile ("PurpleFlareWizard", 40, 0, random (0, 360), 2, random (0, 160))
			TNT1 A 0 A_CustomMissile ("XDeathHalfWizard", 32, 0, random (0, 360), 2, random (0, 160))
		Death:
			TNT1 A 0
			Stop
	}
}
ACTOR XDeathHalfWizard
{
	Radius 8
	Height 8
	Scale 1.0
	Speed 6
	Mass 4
	Decal BrutalBloodSplat
	BounceFactor 0.4
	//+DOOMBOUNCE
	+MISSILE
	+MOVEWITHSECTOR
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOTELEPORT
	States
	{
		Spawn:
			WRIP A 12
			Loop
		Death:
			TNT1 A 0
			WRIP A 1 A_SpawnItem("Gib_HalfWizard",0,0,0,1)
			TNT1 A 0
			Stop
	}
}
Actor Gib_HalfWizard: BigGibBase
{
	States
	{
		Spawn:
			TNT1 AAAAAAAAAAAAAA 0 A_CustomMissile ("XDeathWizardBlack", 32, 0, random (0, 360), 2, random (30, 90))
			TNT1 AAAAAAAAAAAAAAAA 0 A_CustomMissile ("XDeathWizardPurple", 32, 0, random (0, 360), 2, random (30, 90))

			TNT1 AAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BluePlasmaParticleFast", 30, 0, random (0, 360), 2, random (0, 360))
			EXPL AAAAAA 0 A_CustomMissile ("PurpleFireBallExplosionMedium", 6, 0, random (0, 360), 2, random (0, 360))

			TNT1 A 0
			TNT1 A -1
			Stop
	}
}