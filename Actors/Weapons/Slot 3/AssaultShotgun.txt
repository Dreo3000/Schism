Actor RSWasAlreadyInInventory : TokenBase {}

Actor Schism_AssaultShotgunGiver : CustomInventory {
	scale 0.8
	Inventory.PickupMessage "You got the Assault Shotgun! (Slot 3)"
	Inventory.PickupSound "CBOXPKUP"
    States {
		Spawn:
			A12P A -1
			Stop

		Pickup:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("IsSorcerer", 1, "ItsNotForSorcerer")
			TNT1 A 0 A_JumpIfInventory("Schism_AssaultShotgun", 1, "GiveAmmo")
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Schism_CounInvG", 3) >= 2, "DontGive")
			TNT1 A 0 A_GiveInventory("Schism_AssaultShotgun", 1)
			Goto GiveAmmo

		ItsNotForSorcerer:
			TNT1 A 0 A_Print("Sorcerers don't use such primitive kind of firepower", 1)
			Fail

		GiveAmmo:
			TNT1 A 0 A_GiveInventory("AmmoShell", 24)
			Stop

		DontGive:
			TNT1 A 0 A_Print("You can't carry too many shotguns", 1)
			Fail

		DontGiveSecondWeapon:
			TNT1 A 0 A_Print("You don't need one more AssaultShotgun...", 1)
			Fail
    }
}

Actor Schism_AssaultShotgunEmptyGiver : CustomInventory {
	scale 0.8
	Inventory.ForbiddenTo Schism_Corvus
	Inventory.PickupMessage "You got the Assault Shotgun! (Slot 3)"
	Inventory.PickupSound "CBOXPKUP"
    States {
		Spawn:
			A12P A -1
			Stop

		Pickup:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("IsSorcerer", 1, "ItsNotForSorcerer")
			TNT1 A 0 A_JumpIfInventory("Schism_AssaultShotgun", 1, "DontGiveSecondWeapon")
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Schism_CounInvG", 3) >= 2, "DontGive")
			TNT1 A 0 A_GiveInventory("Schism_AssaultShotgun", 1)
			Stop

		ItsNotForSorcerer:
			TNT1 A 0 A_Print("Sorcerers don't use such primitive kind of firepower", 1)
			Fail

		DontGive:
			TNT1 A 0 A_Print("You can't carry too many shotguns", 1)
			Fail

		DontGiveSecondWeapon:
			TNT1 A 0 A_Print("You don't need one more AssaultShotgun...", 1)
			Fail
    }
}

ACTOR Schism_AssaultShotgun : Schism_Weapon {
	Game Doom
// 	SpawnID 28
	Weapon.AmmoType1 "AmmoShell"
	Weapon.AmmoGive 0
	Weapon.AmmoUse 1
	Weapon.SelectionOrder 700
	+WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	+WEAPON.AMMO_OPTIONAL
	Inventory.PickupSound "CBOXPKUP"
	Inventory.ForbiddenTo Schism_Sorcerer, Schism_Corvus
	Inventory.PickupMessage "You got the Auto Rotary Cannon! (Slot 3)"
	Obituary "%o was minced by %k's Auto Rotary Cannon."
	AttackSound "none"
	Tag "Auto Rotary Shotgun"
	Scale 0.9
	States
	{
		Spawn:
			TNT1 A 2
			TNT1 A 1 A_SpawnItem("Schism_AssaultShotgunEmptyGiver", 50)
			Stop

		Select:
			TNT1 A 0 A_Raise
			Wait

		Deselect:
			MSGI A 0 A_StopSound(5)
			MSGI A 0 A_TakeInventory("Spin",1)
			MSGI A 0 A_Takeinventory("HeavyAutomaticWeapon",1)
			MSGI A 0 A_TakeInventory("RandomHeadExploder", 1)
			MSGI A 0 SetPlayerProperty(0, 0, 0)
			PASS A 0 A_JumpIfInventory("IsPaladin",1,2)
			MSGS A 0
			"####" ABCDE 1
			DeselctLoop:
			TNT1 A 0 A_Lower
			Wait

		Ready:
			MSGI A 0 A_PlaySound("CHGNPKUP")
			PRSF A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MRSF A 0
			"####" EDCBA 1
			ReadyLoop:
			PASI A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MSGI A 0
			"####" "#" 0 A_JumpIfInventory("Spin", 1, "SpinReady")
			"####" "#" 0 A_JumpIfInventory("Kicking", 1, "DoKick")
			"####" "#" 1 A_WeaponReady
			Loop

		SpinReady:
			PASI A 0 A_JumpIfInventory("IsPaladin",1,2)
			MSGI A 0
			"####" CDE 1 A_WeaponReady
			"####" "#" 0 A_JumpIfInventory("Kicking",1,"DoKick")
			"####" AB 1 A_WeaponReady
			"####" "#" 0 A_JumpIfInventory("Kicking",1,"DoKick")
			Loop

		Fire:
			"####" "#" 0
			"####" "#" 0 A_JumpIfNoAmmo("NoAmmo")
			"####" "#" 0 A_JumpIfInventory("Spin", 1, "Fire2")
			"####" "#" 0 A_PlaySound("CnonStar", 5, 0.25)
			PASI A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MSGI A 0
			"####" BCDEAB 2
			Hold:
			"####" "#" 0
			"####" "#" 0 A_JumpIfNoAmmo("NoAmmo")
			"####" "#" 0 A_JumpIfInventory("Spin", 1, "Fire2")
		    "####" "#" 0 A_ZoomFactor(0.99)
            "####" "#" 0 A_AlertMonsters
		    "####" "#" 0 A_Recoil(2)
            "####" "#" 0 A_PlaySound("AutoFire", 1)
		    "####" "#" 0 A_FireCustomMissile("DistantFireSoundShotgun", random(-1,1), 0, 0, -12, 0, random(-1,1))
		    "####" "#" 0 A_SpawnItemEx("PlayerMuzzle1", 30, 5, 30)
		    "####" AAAAAAAAAAAAAAAA 0 A_FireCustomMissile("ShotgunParticles", random(-12,12), 0, -1, 0, 0, random(-9,9))
		    "####" AAAAA 0 A_FireCustomMissile("ShotgunParticles2", random(-12,12), 0, -1, 0, 0, random(-9,9))
		    "####" AAAAA 0 A_FireCustomMissile("DecorativeTracer", random(-3,3), 0, 0, -12, 0, random(-3,3))
			"####" "#" 0 A_SetPitch(Pitch-1)

			PASF A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MSGF A 0
			"####" A 1 BRIGHT A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0)
			"####" "#" 0 A_FireBullets(0, 0, 1, 40, "shotpuff", FBF_NORANDOM, 96) // pointblank extra damage
		    "####" "#" 0 A_FireBullets(5, 5, 9, 13, "shotpuff", FBF_NORANDOM | FBF_USEAMMO)
			"####" B 1 BRIGHT

            "####" "#" 0 A_FireCustomMissile("SSGCaseSpawner",0,0,-10, -20)
			"####" "#" 0 A_SpawnItemEx("PlayerMuzzle2",30,5,27)
			"####" "#" 0 A_ZoomFactor(1)
			"####" C 1 Offset(0,33)
			"####" C 1 Offset(0,32)
			"####" "#" 0 A_SetPitch(Pitch+0.5)
			"####" D 1 Offset(0,33)
			"####" D 1 Offset(0,32)
			"####" "#" 0 A_SetPitch(Pitch+0.5)
			"####" E 1 Offset(0,33)
			"####" E 1 Offset(0,32)
			"####" F 1 Offset(0,33)
			"####" F 1 Offset(0,32)
			"####" "#" 0 A_ReFire
			"####" "#" 0 A_PlaySound("ACNLOOP", 5,1,1)
			Goto StopSpin

		AltFire:
			"####" "#" 0 A_JumpIfInventory("Spin", 1, "StopSpin")
			"####" "#" 0 A_PlaySound("CnonStar", 5, 0.25)
			"####" "#" 0 A_GiveInventory("Spin", 1)
			PASI A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MSGI A 0
			"####" BC 2
			"####" DE 2
			"####" AB 2
			"####" "#" 0 A_AlertMonsters
			"####" "#" 0 A_PlaySound("ACNLOOP", 5, 0.25, 1)
			"####" "#" 0
			Goto SpinReady

		Fire2:
            TNT1 A 0
		    SHTN A 0 A_ZoomFactor(0.98)
            SHTN A 0 A_AlertMonsters
            SHTN A 0 A_PlaySound("AutoFire", 1)
		    RIFF A 0 A_FireCustomMissile("DistantFireSoundShotgun", random(-1,1), 0, 0, -12, 0, random(-1,1))
            SHTZ A 0 A_JumpIfInventory("PowerStrength", 1, 2)
		    SHTZ A 0 A_Recoil(4)
		    SHTN A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		    TNT1 AAAAAAAAAAAAAAAA 0 A_FireCustomMissile("ShotgunParticles", random(-12,12), 0, -1, 0, 0, random(-9,9))
		    TNT1 AAAAA 0 A_FireCustomMissile("ShotgunParticles2", random(-12,12), 0, -1, 0, 0, random(-9,9))
		    TNT1 AAAAA 0 A_FireCustomMissile("DecorativeTracer", random(-3,3), 0, 0, -12, 0, random(-3,3))

			PASF A 0 A_JumpIfInventory("IsPaladin",1,2)
			MSGF A 0
			"####" B 1 BRIGHT
			"####" A 0 A_FireBullets (0, 0, 1, 40, "shotpuff",FBF_NORANDOM, 96) // pointblank extra damage
			"####" A 0 A_FireBullets (5, 4, 4, 13, "ShotgunPuff", FBF_NORANDOM)
			"####" A 0 A_FireBullets (6, 4, 5, 13, "ShotgunPuff2", FBF_NORANDOM | FBF_USEAMMO)
			"####" A 0 A_SetPitch(Pitch-8)
			"####" A 0 A_SetAngle(Angle+random(4,-4))
		    "####" B 0 A_FireCustomMissile("ShakeYourAss", 0, 0, 0, 0)

			TNT1 A 0 A_SetPitch(Pitch+1)
            TNT1 A 0 A_FireCustomMissile("SSGCaseSpawner",0,0,-10, -20)
			MSGI A 0 A_SpawnItemEx("PlayerMuzzle2",30,5,27)
			PASG A 0 A_JumpIfInventory("IsPaladin",1,2)
			MGF2 A 0
			"####" B 1 A_ZoomFactor(0.99)
			"####" A 0
			"####" C 1 A_ZoomFactor(1)
			"####" A 0
			"####" D 1 A_SetPitch(Pitch+1)
			"####" E 1 A_SetPitch(Pitch+1)
			PASI A 0 A_JumpIfInventory("IsPaladin",1,2)
			MSGI A 0
			"####" AB 1 A_SetPitch(Pitch+1)
			"####" A 0 A_PlaySound("ACNLOOP", 5,0.25,1)
			"####" A 0 A_ReFire
			"####" CDEAB 1 A_SetPitch(Pitch+0.5)
			TNT1 A 0 A_ReFire
			Goto SpinReady


// 		Вернуть старый код, все бинды должны проходить на каждый фрейм
		StopSpin:
			MSGI A 0 A_TakeInventory("Spin", 1)
			MSGI A 0 A_StopSound(5)
			MSGI A 0 A_JumpIfInventory("Kicking",1,"DoKick")
			MSGI A 0 A_PlaySound("CannonSpinDown", 5, 0.25)
			PASI A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MSGI A 0
			"####" ABCDE 2 A_WeaponReady
			"####" ABCDE 3 A_WeaponReady
			Goto ReadyLoop

		Steady:
			TNT1 A 1
			Goto ReadyLoop

		ClearInv:
			"####" "#" 0 A_TakeInventory("Salute1", 1)
			"####" "#" 0 A_TakeInventory("Salute2", 1)
			Goto ReadyLoop

		NoAmmo:
			MSGI A 0 A_JumpIfInventory("Spin", 1, "StopSpin")
			MSGI A 0 A_PlaySound("weapons/empty")
			MSGI A 2 Offset(0,34)
			MSGI A 1 Offset(0,33)
			MSGI A 1 Offset(0,32)
			Goto ReadyLoop
			
		DoKick: //14 tic
			TNT1 A 0 A_GunFlash("KickFlash")
			TNT1 A 0 A_TakeInventory("Kicking", 1)
			TNT1 A 0 A_TakeInventory("Zoomed", 1)
			TNT1 A 0 A_TakeInventory("PowerLightAmp", 1)
			TNT1 A 0 A_ZoomFactor(1.0)
			TNT1 A 0 A_TakeInventory("ADSmode", 1)
			TNT1 A 0 A_JumpIf (momZ > 0, "KickInAir")
			TNT1 A 0 A_JumpIf (momZ < 0, "KickInAir")
			TNT1 A 0 A_JumpIf((Abs(VelY) < 1) && (Abs(VelX) < 1), 2)
			TNT1 A 0 A_JumpIfInventory("IsCrouching", 1, "SlideAttack")
			TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "DoKickWithBerserk")
			TNT1 A 0 A_PlaySound("KICK")
			TNT1 A 0 SetPlayerProperty(0, 1, 0)
			K1CK A 0 A_JumpIfInventory("IsPaladin", 1, 3)
			K2CK A 0 A_JumpIfInventory("IsSorcerer", 1, 2)
			KICK A 0
			"####" BCD 1
			"####" A 0 A_FireCustomMissile("KickAttack", 0, 0, 0, -7)
			"####" H 3
			"####" IGF 1
			"####" "#" 0 SetPlayerProperty(0, 0, 0)
			"####" EDCBA 1 A_JumpIfInventory("Kicking", 1, "KickRight")
			TNT1 A 0 A_JumpIfInventory("Kicking", 1, "KickRight")
			Goto ReadyLoop


		DoKickWithBerserk:
			TNT1 A 0 A_PlaySound("KICK")
			K1CK A 0 A_JumpIfInventory("IsPaladin", 1, 3)
			K2CK A 0 A_JumpIfInventory("IsSorcerer", 1, 2)
			KICK A 0
			"####" BCD 1
			"####" A 0 A_FireCustomMissile("SuperKickAttack", 0, 0, 0, -7)
			"####" H 3
			"####" IGF 1
			"####" ED 1
			"####" CBA 1 A_JumpIfInventory("Kicking", 1, "KickRight")
			TNT1 A 0 A_JumpIfInventory("Kicking", 1, "KickRight")
			Goto ReadyLoop

		KickRight: // 15 tic
			TNT1 A 0 A_GunFlash("RightKickFlash")
			K1K2 A 0 A_JumpIfInventory("IsPaladin", 1, 3)
			K2K2 A 0 A_JumpIfInventory("IsSorcerer", 1, 2)
			KIK2 A 0
			"####" "#" 0 A_TakeInventory("Kicking", 1)
			"####" ABC 1
			"####" A 0 A_PlaySound("KICK")
			"####" A 0
			"####" A 0 A_SetPitch(pitch-9)
			"####" A 0 A_SetAngle(angle+9)
			"####" A 0 A_FireCustomMissile("KickAttack", 0, 0, 0, 0)
			"####" D 1
			"####" A 0 A_SetPitch(pitch+2)
			"####" A 0 A_SetAngle(angle-2)
			"####" D 1
			"####" A 0 A_SetPitch(pitch+2)
			"####" A 0 A_SetAngle(angle-2)
			"####" E 1
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle-1)
			"####" E 1
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle-1)
			"####" C 1 A_JumpIfInventory("Kicking", 1, "KickInAir")
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle-1)
			"####" B 2 A_JumpIfInventory("Kicking", 1, "KickInAir")
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle-1)
			"####" A 2 A_JumpIfInventory("Kicking", 1, "KickInAir")
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle-1)
			"####" A 2 A_JumpIfInventory("Kicking", 1, "KickInAir")
			"####" A 1 A_JumpIfInventory("Kicking", 1, "KickInAir")
			Goto ReadyLoop

		KickInAir: // 18 tics
			TNT1 A 0 A_GunFlash("KickInAirFlash")
			TNT1 A 0 A_TakeInventory("Kicking", 1)
			K1CK J 0 A_JumpIfInventory("IsPaladin", 1, 3)
			K2CK J 0 A_JumpIfInventory("IsSorcerer", 1, 2)
			KICK J 1
			"####" A 0 A_JumpIfInventory("PowerStrength", 1, "KickInAirWithBerserk")
			"####" A 0 A_PlaySound("KICK")
			"####" A 0 A_Recoil(-15)
			"####" JKLMN 1
			"####" A 0 A_FireCustomMissile("AirKickAttack", 0, 0, 0, -31)
			"####" O 3
			"####" PQ 2
			"####" RS 1
			"####" T 1 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			"####" A 2 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			"####" A 1 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			Goto ReadyLoop

		KickInAirWithBerserk: //18 tics
			K1CK J 0 A_JumpIfInventory("IsPaladin", 1, 3)
			K2CK J 0 A_JumpIfInventory("IsSorcerer", 1, 2)
			KICK J 0
			"####" A 0 A_PlaySound("KICK")
			"####" A 0 A_Recoil (-15)
			"####" JKLMN 1
			"####" A 0 A_FireCustomMissile("SuperAirKickAttack", 0, 0, 0, -31)
			"####" O 3
			"####" PQ 2
			"####" RS 1
			"####" T 1 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			"####" A 2 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			"####" A 1 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			Goto ReadyLoop

		LeftFinishingKick: // 11 tics
			TNT1 A 0 A_GunFlash("LeftKickFlash")
			K1K3 A 0 A_JumpIfInventory("IsPaladin", 1, 3)
			K2K3 A 0 A_JumpIfInventory("IsSorcerer", 1, 2)
			KIK3 A 0
			"####" "#" 0 A_TakeInventory("Kicking", 1)
			"####" A 0 A_Recoil(-10)
			"####" ABC 1
			"####" A 0 A_PlaySound("KICK")
			"####" A 0 A_SetPitch(pitch-9)
			"####" A 0 A_SetAngle(angle-9)
			"####" D 1 A_FireCustomMissile("KickAttack", 0, 0, 0, 0)
			"####" A 0 A_SetPitch(pitch+2)
			"####" A 0 A_SetAngle(angle+3)
			"####" E 1
			"####" A 0 A_SetPitch(pitch+2)
			"####" A 0 A_SetAngle(angle+3)
			"####" F 1 A_FireCustomMissile("KickAttack", 0, 0, 0, 0)
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle+3)
			"####" G 1
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle+2)
			"####" H 1 A_FireCustomMissile("KickAttack", 0, 0, 0, 0)
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle+2)
			"####" I 1
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle+1)
			"####" J 1 A_FireCustomMissile("KickAttack", 0, 0, 0, 0)
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle+1)
			"####" K 1
			Goto ReadyLoop

		SlideAttack:
			TNT1 A 0 A_GunFlash("SlideKickFlashStart")
			TNT1 A 0 SetPlayerProperty(0,1,0)
			KICK A 0 A_TakeInventory("Kicking",1)
			TNT1 A 0 A_PlaySound("KICK")
			TNT1 A 0 A_Recoil (-16)
			TNT1 A 0 A_PlaySound("SlideKickStart", 5)
			S1DK A 0 A_JumpIfInventory("IsPaladin",1,3)
			S2DK A 0 A_JumpIfInventory("IsSorcerer",1,2)
			SLDK A 1
			"####" ABC 1
			"####" A 0 A_SetPItch(10)
			"####" A 0 A_FireCustomMissile("SlideAttack", 0, 0, 0, 10)
			"####" E 1
			"####" A 0 A_Recoil (-14)
			"####" A 0 A_SetPItch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide
			"####" F 1 A_FireCustomMissile("SlideAttack", 0, 0, 0, 10)
			"####" A 0 A_Recoil (-12)
			"####" A 0 A_SetPItch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" G 1 A_FireCustomMissile("SlideAttack", 0, 0, 0, 10)
			"####" A 0 A_Recoil (-8)
			"####" A 0 A_SetPItch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" F 1 A_FireCustomMissile("SlideAttack", 0, 0, 0, 10)
			"####" A 0 A_Recoil (-6)
			"####" A 0 A_SetPItch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" G 1 A_FireCustomMissile("SlideAttack", 0, 0, 0, 10)
			"####" A 0 A_Recoil (-4)
			"####" A 0 A_SetPItch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" F 1 A_FireCustomMissile("SlideAttack", 0, 0, 0, 10)
			"####" A 0 A_Recoil (-4)
			"####" A 0 A_SetPItch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" G 1 A_FireCustomMissile("SlideAttack", 0, 0, 0, 10)
			"####" A 0 A_Recoil (-3)
			"####" A 0 A_SetPItch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" F 1 A_FireCustomMissile("SlideAttack", 0, 0, 0, 10)
			"####" A 0 A_Recoil (-3)
			"####" A 0 A_SetPItch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" G 1 A_FireCustomMissile("SlideAttack", 0, 0, 0, 10)
			"####" A 0 A_Recoil (-3)
			"####" A 0 A_SetPItch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" F 1 A_FireCustomMissile("SlideAttack", 0, 0, 0, 10)
			"####" A 0 A_Recoil (-2)
			"####" A 0 A_SetPItch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" G 1 A_FireCustomMissile("SlideAttack", 0, 0, 0, 10)
			"####" A 0 A_Recoil (-1)
			"####" A 0 A_SetPItch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" F 1 A_FireCustomMissile("SlideAttack", 0, 0, 0, 10)
			"####" A 0 A_Recoil (-1)
			"####" A 0 A_SetPItch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" G 1 A_FireCustomMissile("SlideAttack", 0, 0, 0, 10)
			"####" A 0 A_Recoil (-2)
			"####" A 0 A_SetPItch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			"####" A 0 A_TakeInventory("KickHasHit", 1)
			Goto FinishSlide

		FinishSlide:  //10 tic
			TNT1 A 0 A_TakeInventory("Kicking",1)
			TNT1 A 0 A_GunFlash("SlideKickFlashEnd")
			TNT1 A 0 A_PlaySound("SlideKickStop", 5)
			S1DK H 0 A_JumpIfInventory("IsPaladin",1,3)
			S2DK H 0 A_JumpIfInventory("IsSorcerer",1,2)
			SLDK H 0
			"####" A 0 SetPlayerProperty(0,0,0)
			"####" A 0 A_JumpIfInventory("IsPaladin",1,2)
			"####" A 0 A_Jump(256,5)
			"####" A 0 A_JumpIfInventory("FiredSecondary",1,2)
			"####" A 0 A_Jump(256,3)
			"####" A 0 A_GiveInventory("DoUppercutNow",1)
			"####" A 0 A_SelectWeapon("Berserker_Melee_Attacks")
			"####" HD 2 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			"####" A 0 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			"####" CBA 2 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			"####" A 0 A_TakeInventory("Kicking",1)
			Goto ReadyLoop

		FinishSlideHitwall:
			TNT1 A 0 A_GunFlash("SlideKickFlashEnd")
			S1DK H 0 A_JumpIfInventory("IsPaladin",1,3)
			S2DK H 0 A_JumpIfInventory("IsSorcerer",1,2)
			SLDK H 1
			"####" A 0 A_Stop
			"####" HDCBA 2
			"####" A 0 A_TakeInventory("KickHasHit", 1)
			"####" A 0 A_TakeInventory("Kicking",1)
			"####" A 0 SetPlayerProperty(0,0,0)
			Goto ReadyLoop

		KickFlash: // 14 tics
			PRSF A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MRSF A 0
			"####" ABCDEEEEEEDCBA 1
			Stop

		LeftKickFlash: // 11 tics
			PRSF A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MRSF A 0
			"####" EEEEEEEDCBA 1
			Stop

		RightKickFlash: // 15 tics
			PRSF A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MRSF A 0
			"####" EEEEEEEEEEEDCBA 1
			Stop

		KickInAirFlash: // 19 tics
			PRSF A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MRSF A 0
			"####" ABCDEEEEEEEEEEEDCBA 1
			Stop

		SlideKickFlashStart: // 39 tics
			TNT1 A 39
			Stop

		SlideKickFlashEnd: // 10 tics
			TNT1 A 10
			Stop

		GrenadeFlash: // 24 tics
			PRSF A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MRSF A 0
			"####" ABCDE 1
			TNT1 A 19
			Stop
	}
}