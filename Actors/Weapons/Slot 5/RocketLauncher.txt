Actor Schism_RocketLauncherGiver : Schism_BaseWeaponGiver
{
	scale 0.8
	Inventory.PickupSound "ROCKPKUP"
	Inventory.PickupMessage "$GOTLAUNCHER"
	Inventory.ForbiddenTo Schism_Corvus, Schism_Sorcerer, BaronPlayer, RevenantPlayer
    States
    {
		Spawn:
			RLAU A -1
			Stop

		Pickup:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("Schism_RocketLauncher", 1, "GiveAmmo")
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Schism_CounInvG", 5) >= 2, "DontGive")
			TNT1 A 0 A_GiveInventory("Schism_RocketLauncher", 1)
			Goto GiveAmmo

		GiveAmmo:
			TNT1 A 0 A_GiveInventory("AmmoRocket", 2)
			Stop

		DontGive:
			LAUN A 30
			Fail
    }
}

Actor Schism_RocketLauncherEmptyGiver : Schism_BaseWeaponGiver
{
	scale 0.8
	Inventory.PickupSound "ROCKPKUP"
	Inventory.PickupMessage "$GOTLAUNCHER"
	Inventory.ForbiddenTo Schism_Corvus, Schism_Sorcerer, BaronPlayer, RevenantPlayer
    States
    {
		Spawn:
			RLAU A -1
			Stop

		Pickup:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("Schism_RocketLauncher", 1, "DontGive")
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Schism_CounInvG", 5) >= 2, "DontGive")
			TNT1 A 0 A_GiveInventory("Schism_RocketLauncher", 1)
			Stop

		DontGive:
			LAUN A 30
			Fail
    }
}

Actor Schism_RocketLauncher : Schism_Weapon
{
	Weapon.AmmoType1 "Schism_RocketLauncherMag"
	Weapon.AmmoGive1 0
	Weapon.AmmoUse1 1
	Weapon.AmmoType2 "AmmoRocket"
	Weapon.AmmoGive2 0
	Weapon.AmmoUse2 0
	Weapon.SelectionOrder 9500
	+WEAPON.NOAUTOAIM
	+WEAPON.EXPLOSIVE
	+WEAPON.NOAUTOFIRE
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	+INVENTORY.RESTRICTABSOLUTELY
	Inventory.PickupSound "ROCKPKUP"
	Inventory.PickupMessage "$GOTLAUNCHER"
	Inventory.ForbiddenTo Schism_Sorcerer, Schism_Corvus
	Tag "Rocket Launcher"
	States
	{
		Spawn:
			TNT1 A 2
			TNT1 A 1 A_SpawnItem("Schism_RocketLauncherEmptyGiver", 50)
			Stop

		Select:
			TNT1 A 0 A_Raise
			Wait

		Deselect:
			RLNG A 0
			"####" "#" 0 A_GunFlash("LightDone")
			RLNG A 0 A_JumpIfInventory("Zoomed", 1, "Deselect2")
			PMSS A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MISS A 0
			"####" DCBA 1
			DeselectLoop:
			TNT1 A 0 A_Lower
			Wait

		Deselect2:
			RLNG A 0 A_Takeinventory("Zoomed",1)
			RLNG A 0 A_Takeinventory("PowerLightAmp", 1)
			RLNG A 0 A_ZoomFactor(1.0)
			PMSS A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MISS A 0
			"####" DCBA 1
			Deselect2Loop:
			TNT1 A 0 A_Lower
			Wait

		Ready:
			TNT1 A 0 A_PlaySound("RLANDRAW")
			PMSS A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MISS A 0
			"####" ABCDEF 1 A_WeaponReady(WRF_NOFIRE)
			ReadyLoop:
			PRLG A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			RLNG A 0
			"####" "#" 0 A_GunFlash("LightDone")
			"####" "#" 0 A_JumpIfInventory("GoFatality", 1, "Steady")
			"####" "#" 0 A_JumpIfInventory("Reloading", 1, "Reload")
			"####" "#" 0 A_JumpIfInventory("Kicking", 1, "DoKick")
			"####" "#" 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop

		ReadyInZoom:
			SNIP C 1 BRIGHT A_WeaponReady(WRF_ALLOWRELOAD)
			"####" "#" 0 A_JumpIfInventory("FiredPrimary", 1, "Fire")
			"####" "#" 0 A_JumpIfInventory("Kicking", 1, "DoKick")
			"####" "#" 0 A_JumpIfInventory("Reloading", 1, "Reload")
			"####" "#" 0 A_JumpIfInventory("GoFatality", 1, "Steady")
			Loop

		Fire:
			PRLG A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			RLNG A 0
			"####" A 0 A_JumpIfNoAmmo("NoAmmo")
			"####" A 0 A_AlertMonsters
			"####" A 0 A_GunFlash("Flash")
			"####" A 0 A_JumpIfInventory("Zoomed", 1, "FireInZoom")
			"####" A 1
			"####" A 0 A_JumpIfInventory("DMgame", 1, "DM")
			"####" A 0 A_JumpIfInventory("HellishMissileLauncher", 1, "SynergyFire")
			"####" A 0 A_FireCustomMissile("RedFlareSpawn", -5, 0, 0, 0)
			"####" A 0 A_PlaySound ("DSRFIRE")
			"####" A 0 A_FireCustomMissile("Alerter", 0, 0)
			"####" A 0 A_ZoomFactor(0.98)
			PRLF A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			RLNF A 0
			"####" A 1 BRIGHT
			"####" A 0 A_FireCustomMissile("Rocket2", 0, 1, 0, -6)
			"####" A 0 A_FireCustomMissile("RocketGlassBreaker", 0, 0, 0, -6)
			Goto RecoilAnimation

			DM:
			PRLF A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			RLNF A 0
			"####" A 0 A_FireCustomMissile("RedFlareSpawn",-5,0,0,0)
			"####" A 0 A_PlaySound ("DSRFIRE")
			"####" A 0 A_ZoomFactor(0.98)
			"####" A 1 BRIGHT A_FireCustomMissile("RocketDM", 0, 1, 0, -6)
			"####" A 0 A_FireCustomMissile("RocketGlassBreaker", 0, 0, 0, -6)
			Goto RecoilAnimation

			SynergyFire:
			PRLF A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			RLNF A 0
			"####" A 0 A_FireCustomMissile("RedFlareSpawn", -5, 0, 0, 0)
			"####" A 0 A_PlaySound("DSRFIRE")
			"####" A 0 A_ZoomFactor(0.98)
			"####" A 1 BRIGHT A_FireCustomMissile("SeekerRocket2", 0, 1, 0, -6)
			"####" A 0 A_FireCustomMissile("RocketGlassBreaker", 0, 0, 0, -6)
			Goto RecoilAnimation

			RecoilAnimation:
			PRLF A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			RLNF A 0
			"####" A 0 A_ZoomFactor(1.0)
			"####" B 1 BRIGHT A_WeaponReady(WRF_NOFIRE| WRF_NOBOB) // Allows quick switch
			"####" C 1
			"####" D 1 A_WeaponReady(WRF_NOFIRE| WRF_NOBOB) // Allows quick switch
			"####" E 1
			"####" FGH 1

			PRLG A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			RLNG A 0
			"####" A 0 A_JumpIfInventory("Reloading", 1, "Reload")
			"####" AAA 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)
			"####" A 0 A_JumpIfInventory("Reloading", 1, "Reload")
			"####" BCD 2 A_WeaponReady(WRF_NOFIRE| WRF_NOBOB | WRF_ALLOWRELOAD) // Allows quick switch
			"####" A 1
			"####" A 0 A_Refire
			Goto ReadyLoop

		Flash:
		    TNT1 A 1 A_Light2
		    TNT1 A 1 A_Light1
		    Goto LightDone

		FireInZoom:
			SNIP C 1
			RLNG A 0 A_JumpIfInventory("DMgame", 1, "FireInZoomDM")
			RLNG A 0 A_JumpIfInventory("HellishMissileLauncher", 1, "ZoomedSynergyFire")
			RLNG A 0 A_FireCustomMissile("RedFlareSpawn", -5, 0, 0, 0)
			RLNG A 0 A_PlaySound("DSRFIRE")
			RLNG A 0 A_FireCustomMissile("Alerter", 0, 0)
			RLNG A 0 A_ZoomFactor(2.9)
			SNIP C 1 BRIGHT
			RLNG A 0 A_FireCustomMissile("Rocket2", 0, 0, 0, -6)
			RLNG A 0 A_FireCustomMissile("RocketGlassBreaker", 0, 0, 0, -6)
			Goto ZoomedRecoil

			FireInZoomDM:
			RLNG A 0 A_FireCustomMissile("RedFlareSpawn", -5, 0, 0, 0)
			RLNG A 0 A_PlaySound ("DSRFIRE")
			RLNG A 0 A_ZoomFactor(2.9)
			SNIP C 1 BRIGHT A_FireCustomMissile("RocketDM", 0, 0, 0, -6)
			RLNG A 0 A_FireCustomMissile("RocketGlassBreaker", 0, 0, 0, -6)
			Goto ZoomedRecoil

			ZoomedSynergyFire:
			RLNG A 0 A_FireCustomMissile("RedFlareSpawn", -5, 0, 0, 0)
			RLNG A 0 A_PlaySound ("DSRFIRE")
			RLNG A 0 A_ZoomFactor(2.9)
			SNIP C 1 BRIGHT A_FireCustomMissile("SeekerRocket2", 0, 0, 0, -6)
			RLNG A 0 A_FireCustomMissile("RocketGlassBreaker", 0, 0, 0, -6)
			Goto ZoomedRecoil

			ZoomedRecoil:
			RLNG A 0 A_Takeinventory("Schism_RocketLauncherMag", 1)
			RLNG A 0 A_ZoomFactor(3.0)
			SNIP C 1 BRIGHT
			SNIP C 1 BRIGHT A_SetPitch(-0.6 + pitch)
			SNIP C 1 BRIGHT
			SNIP C 1 BRIGHT
			SNIP CCC 1 BRIGHT A_SetPitch(0.1 + pitch)
			SNIP CCC 1 BRIGHT A_SetPitch(0.1 + pitch)
			SNIP CCC 2 BRIGHT
			SNIP C 1 BRIGHT
			RLNG A 0 A_JumpIfInventory("FiredPrimary", 1, "Fire")
			RLNG A 0 A_JumpIfInventory("FiredSecondary", 1, "ADSMode")//If still pressing alt fire, it means its holding aim
			RLNG A 0 A_Refire
			Goto ReadyInZoom

		AltFire:
			RLNG A 0
			RLNG A 0 A_JumpIfInventory("Zoomed", 1, "ExitFromZoom")
			RLNG A 0 A_Giveinventory("Zoomed", 1)
			RLNG A 0 A_ZoomFactor(3.0)
			RLNG A 0 A_giveinventory("PowerLightAmp", 1)
			PMAI A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MIAI A 0
			"####" ABCDE 1
			RLNG A 0 A_JumpIfInventory("FiredPrimary", 1, "Fire")
			SNIP C 1 A_WeaponReady(WRF_ALLOWRELOAD)
			RLNG A 0 A_JumpIfInventory("FiredPrimary", 1, "Fire")
			SNIP C 1 A_WeaponReady(WRF_ALLOWRELOAD)
			RLNG A 0 A_JumpIfInventory("FiredPrimary", 1, "Fire")
			SNIP C 1 A_WeaponReady(WRF_ALLOWRELOAD)
			RLNG A 0 A_JumpIfInventory("FiredPrimary", 1, "Fire")
			RLNG A 0 A_JumpIfInventory("FiredSecondary", 1, "ADSMode")
			Goto ReadyInZoom

		 ADSMode:
			RLNG A 0 A_JumpIfInventory("FiredPrimary", 1, "Fire")
			SNIP C 1 A_WeaponReady(WRF_ALLOWRELOAD)
			RLNG A 0 A_JumpIfInventory("FiredPrimary", 1, "Fire")
			SNIP C 1 A_WeaponReady(WRF_ALLOWRELOAD)
			RLNG A 0 A_JumpIfInventory("FiredPrimary", 1, "Fire")
			SNIP C 1 A_WeaponReady(WRF_ALLOWRELOAD)
			RLNG A 0 A_JumpIfInventory("FiredSecondary", 1, "ADSMode")
			Goto ExitFromZoom

		ExitFromZoom:
			PMAI A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MIAI A 0
			"####" EDBA 1
			RLNG A 0 A_Light0
			RLNG A 0 A_Takeinventory("Zoomed",1)
			RLNG A 0 A_Takeinventory("PowerLightAmp", 1)
			RLNG A 0 A_ZoomFactor(1.0)
			Goto ReadyLoop

		Reload:
			RLNG A 0
			RLNG A 0 A_TakeInventory("Reloading", 1)
			RLNG A 0 A_ZoomFactor(1.0)
			RLNG A 0 A_TakeInventory("Zoomed", 1)
			RLNG A 0 A_TakeInventory("PowerLightAmp", 1)
			RLNG A 0 A_JumpIfInventory("Schism_RocketLauncherMag", 6, "ReadyLoop")
			RLNG A 0 A_JumpIfInventory("AmmoRocket", 1, 1)
			Goto ReadyLoop
			RLNG A 0 A_GunFlash("FlashReload")
			"####" "#" 0 A_GiveInventory("Reloading2", 1)
			ReloadLoop:
			"TNT1" "A" 1 A_JumpIfInventory("Kicking", 1, "DoKick")
			"####" "#" 0 A_JumpIfInventory("Reloading2", 1, "ReloadLoop")
			Goto ReadyLoop

		FlashReload:
			PMSR A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MISR A 0
			"####" A 0 A_PlaySound("Reload")
			"####" ABCDE 1
			"####" A 0 A_FireCustomMissile("RocketCaseSpawn",-20,0,2,-10)
			"####" EFGGG 1
			"####" HIJJJJKLM 1
			"####" MNOPONM 1
			"####" Q 1
			"####" R 1
			"####" BCD 2
			"####" A 1
			Goto RefillTo6

		RefillTo6:
			"####" "#" 0 A_TakeInventory("AmmoRocket", 1, TIF_NOTAKEINFINITE)
			"####" "#" 0 A_GiveInventory("Schism_RocketLauncherMag", 1)
			"####" "#" 0 A_JumpIfInventory("Schism_RocketLauncherMag", 6, "FinishReload")
			"####" "#" 0 A_JumpIfInventory("AmmoRocket", 1, "RefillTo6")
			Goto FinishReload

		FinishReload:
		    PRLG A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			RLNG A 0
			"####" "#" 0 A_TakeInventory("Reloading2", 1)
			"####" "#" 0 A_Refire
			"####" "#" 2100
		    Stop

		NoAmmo:
			PMSR J 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MISR J 0
			"####" "#" 0 A_Takeinventory("PowerLightAmp", 1)
			"####" "#" 0 A_ZoomFactor(1.0)
			"####" "#" 0 A_Takeinventory("Zoomed",1)
			"####" "#" 0 A_Takeinventory("ADSmode",1)
			"####" "#" 0 A_PlaySound("weapons/empty", 4)
			NoAmmo2:
			"####" "#" 0 A_JumpIfInventory("Kicking", 1 ,"DoKick")
			"####" "#" 5 A_WeaponReady(WRF_ALLOWRELOAD | WRF_NOPRIMARY)
			"####" "#" 0 A_JumpIfInventory("Reloading", 1, "Reload")
			"####" "#" 0 A_JumpIfInventory("NoAutoReload", 1, "NoAmmo2")
			"####" "#" 0 A_JumpIfInventory("AmmoRocket", 1, "Reload")
			"####" "#" 0 A_JumpIfInventory("FiredPrimary", 1, "NoAmmo2")
			Goto ReadyLoop

		Steady:
			TNT1 A 1
			Goto ReadyLoop

		DoKick: //14 tic
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("Reloading2", 1, 2)
			TNT1 A 0 A_GunFlash("KickFlash")
			TNT1 A 0 A_TakeInventory("Kicking", 1)
			TNT1 A 0 A_TakeInventory("Zoomed", 1)
			TNT1 A 0 A_ZoomFactor(1.0)
			TNT1 A 0 A_TakeInventory("ADSmode", 1)
			TNT1 A 0 A_JumpIf (momZ > 0, "KickInAir")
			TNT1 A 0 A_JumpIf (momZ < 0, "KickInAir")
			TNT1 A 0 A_JumpIf((Abs(VelY) < 1) && (Abs(VelX) < 1), 2)
			TNT1 A 0 A_JumpIfInventory("IsCrouching", 1, "SlideAttack")
			TNT1 A 0 A_PlaySound("KICK")
			TNT1 A 0 SetPlayerProperty(0, 1, 0)
			K1CK A 0 A_JumpIfInventory("IsPaladin", 1, 3)
			K2CK A 0 A_JumpIfInventory("IsSorcerer", 1, 2)
			KICK A 0
			"####" BCD 1
			"####" A 0 A_GiveInventory("Schism_Kick", 1)
			"####" H 3
			"####" IGF 1
			"####" "#" 0 SetPlayerProperty(0, 0, 0)
			"####" EDCBA 1 A_JumpIfInventory("Kicking", 1, "KickRight")
			TNT1 A 0 A_JumpIfInventory("Kicking", 1, "KickRight")
			"####" "#" 0 A_JumpIfInventory("Reloading2", 1, "ReloadLoop")
			Goto ReadyLoop

		KickRight: // 15 tic
			TNT1 A 0 A_JumpIfInventory("Reloading2", 1, 2)
			TNT1 A 0 A_GunFlash("RightKickFlash")
			K1K2 A 0 A_JumpIfInventory("IsPaladin", 1, 3)
			K2K2 A 0 A_JumpIfInventory("IsSorcerer", 1, 2)
			KIK2 A 0
			"####" "#" 0 A_TakeInventory("Kicking", 1)
			"####" ABC 1
			"####" A 0 A_PlaySound("KICK")
			"####" A 0
			"####" A 0 A_SetPitch(pitch-9)
			"####" A 0 A_SetAngle(angle+9)
			"####" A 0 A_GiveInventory("Schism_Kick", 1)
			"####" D 1
			"####" A 0 A_SetPitch(pitch+2)
			"####" A 0 A_SetAngle(angle-2)
			"####" D 1
			"####" A 0 A_SetPitch(pitch+2)
			"####" A 0 A_SetAngle(angle-2)
			"####" E 1
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle-1)
			"####" E 1
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle-1)
			"####" C 1 A_JumpIfInventory("Kicking", 1, "KickInAir")
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle-1)
			"####" B 2 A_JumpIfInventory("Kicking", 1, "KickInAir")
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle-1)
			"####" A 2 A_JumpIfInventory("Kicking", 1, "KickInAir")
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle-1)
			"####" A 2 A_JumpIfInventory("Kicking", 1, "KickInAir")
			"####" A 1 A_JumpIfInventory("Kicking", 1, "KickInAir")
			"####" "#" 0 A_JumpIfInventory("Reloading2", 1, "ReloadLoop")
			Goto ReadyLoop

		KickInAir: // 18 tics
			TNT1 A 0 A_JumpIfInventory("Reloading2", 1, 2)
			TNT1 A 0 A_GunFlash("KickInAirFlash")
			TNT1 A 0 A_TakeInventory("Kicking", 1)
			K1CK J 0 A_JumpIfInventory("IsPaladin", 1, 3)
			K2CK J 0 A_JumpIfInventory("IsSorcerer", 1, 2)
			KICK J 1
			"####" A 0 A_PlaySound("KICK")
			"####" A 0 A_Recoil(-15)
			"####" JKLMN 1
			"####" A 0 A_GiveInventory("Schism_AirKick", 1)
			"####" O 3
			"####" PQ 2
			"####" RS 1
			"####" T 1 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			"####" A 2 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			"####" A 1 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			"####" "#" 0 A_JumpIfInventory("Reloading2", 1, "ReloadLoop")
			Goto ReadyLoop

		LeftFinishingKick: // 11 tics
			TNT1 A 0 A_JumpIfInventory("Reloading2", 1, 2)
			TNT1 A 0 A_GunFlash("LeftKickFlash")
			K1K3 A 0 A_JumpIfInventory("IsPaladin", 1, 3)
			K2K3 A 0 A_JumpIfInventory("IsSorcerer", 1, 2)
			KIK3 A 0
			"####" "#" 0 A_TakeInventory("Kicking", 1)
			"####" A 0 A_Recoil(-10)
			"####" ABC 1
			"####" A 0 A_PlaySound("KICK")
			"####" A 0 A_SetPitch(pitch-9)
			"####" A 0 A_SetAngle(angle-9)
			"####" D 1 A_GiveInventory("Schism_Kick", 1)
			"####" A 0 A_SetPitch(pitch+2)
			"####" A 0 A_SetAngle(angle+3)
			"####" E 1
			"####" A 0 A_SetPitch(pitch+2)
			"####" A 0 A_SetAngle(angle+3)
			"####" F 1 A_GiveInventory("Schism_Kick", 1)
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle+3)
			"####" G 1
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle+2)
			"####" H 1 A_GiveInventory("Schism_Kick", 1)
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle+2)
			"####" I 1
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle+1)
			"####" J 1 A_GiveInventory("Schism_Kick", 1)
			"####" A 0 A_SetPitch(pitch+1)
			"####" A 0 A_SetAngle(angle+1)
			"####" K 1
			"####" "#" 0 A_JumpIfInventory("Reloading2", 1, "ReloadLoop")
			Goto ReadyLoop

		SlideAttack:
			TNT1 A 0 A_JumpIfInventory("Reloading2", 1, 2)
			TNT1 A 0 A_GunFlash("SlideKickFlashStart")
			TNT1 A 0 SetPlayerProperty(0, 1, 0)
			KICK A 0 A_TakeInventory("Kicking", 1)
			TNT1 A 0 A_PlaySound("KICK")
			TNT1 A 0 A_Recoil(-16)
			TNT1 A 0 A_PlaySound("SlideKickStart", CHAN_5)
			S1DK A 0 A_JumpIfInventory("IsPaladin", 1, 3)
			S2DK A 0 A_JumpIfInventory("IsSorcerer", 1, 2)
			SLDK A 1
			"####" ABC 1
			"####" A 0 A_SetPitch(10)
			"####" A 0 A_GiveInventory("Schism_SlideKick", 1)
			"####" E 1
			"####" A 0 A_Recoil(-14)
			"####" A 0 A_SetPitch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide
			"####" F 1 A_GiveInventory("Schism_SlideKick", 1)
			"####" A 0 A_Recoil(-12)
			"####" A 0 A_SetPitch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" G 1 A_GiveInventory("Schism_SlideKick", 1)
			"####" A 0 A_Recoil(-8)
			"####" A 0 A_SetPitch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" F 1 A_GiveInventory("Schism_SlideKick", 1)
			"####" A 0 A_Recoil(-6)
			"####" A 0 A_SetPitch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" G 1 A_GiveInventory("Schism_SlideKick", 1)
			"####" A 0 A_Recoil(-4)
			"####" A 0 A_SetPitch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" F 1 A_GiveInventory("Schism_SlideKick", 1)
			"####" A 0 A_Recoil(-4)
			"####" A 0 A_SetPitch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" G 1 A_GiveInventory("Schism_SlideKick", 1)
			"####" A 0 A_Recoil(-3)
			"####" A 0 A_SetPitch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" F 1 A_GiveInventory("Schism_SlideKick", 1)
			"####" A 0 A_Recoil(-3)
			"####" A 0 A_SetPitch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" G 1 A_GiveInventory("Schism_SlideKick", 1)
			"####" A 0 A_Recoil(-3)
			"####" A 0 A_SetPitch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" F 1 A_GiveInventory("Schism_SlideKick", 1)
			"####" A 0 A_Recoil(-2)
			"####" A 0 A_SetPitch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" G 1 A_GiveInventory("Schism_SlideKick", 1)
			"####" A 0 A_Recoil(-1)
			"####" A 0 A_SetPitch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" F 1 A_GiveInventory("Schism_SlideKick", 1)
			"####" A 0 A_Recoil(-1)
			"####" A 0 A_SetPitch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			Goto FinishSlide

			"####" G 1 A_GiveInventory("Schism_SlideKick", 1)
			"####" A 0 A_Recoil(-2)
			"####" A 0 A_SetPitch(10)
			"####" A 0 A_JumpIfInventory("KickHasHit", 1, "FinishSlideHitWall")
			"####" A 0 A_JumpIfInventory("IsCrouching", 1, 1)
			"####" A 0 A_TakeInventory("KickHasHit", 1)
			Goto FinishSlide

		FinishSlide:  //10 tic
			TNT1 A 0 A_TakeInventory("Kicking",1)
			TNT1 A 0 A_JumpIfInventory("Reloading2", 1, 2)
			TNT1 A 0 A_GunFlash("SlideKickFlashEnd")
			TNT1 A 0 A_PlaySound("SlideKickStop", CHAN_5)
			S1DK H 0 A_JumpIfInventory("IsPaladin", 1, 3)
			S2DK H 0 A_JumpIfInventory("IsSorcerer", 1, 2)
			SLDK H 0
			"####" A 0 SetPlayerProperty(0, 0, 0)
			"####" A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			"####" A 0 A_Jump(256,5)
			"####" A 0 A_JumpIfInventory("FiredSecondary", 1, 2)
			"####" A 0 A_Jump(256,3)
			"####" A 0 A_GiveInventory("DoUppercutNow", 1)
			"####" A 0 A_SelectWeapon("Berserker_Melee_Attacks")
			"####" HD 2 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			"####" A 0 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			"####" CBA 2 A_JumpIfInventory("Kicking", 1, "LeftFinishingKick")
			"####" A 0 A_Takeinventory("Kicking", 1)
			"####" "#" 0 A_JumpIfInventory("Reloading2", 1, "ReloadLoop")
			Goto ReadyLoop

		FinishSlideHitwall:
			TNT1 A 0 A_JumpIfInventory("Reloading2", 1, 2)
			TNT1 A 0 A_GunFlash("SlideKickFlashEnd")
			S1DK H 0 A_JumpIfInventory("IsPaladin", 1, 3)
			S2DK H 0 A_JumpIfInventory("IsSorcerer", 1, 2)
			SLDK H 0
			"####" A 0 A_Stop
			"####" HDCBA 2
			"####" A 0 A_TAkeInventory("KickHasHit", 1)
			"####" A 0 A_Takeinventory("Kicking", 1)
			"####" A 0 SetPlayerProperty(0, 0, 0)
			"####" "#" 0 A_JumpIfInventory("Reloading2", 1, "ReloadLoop")
			Goto ReadyLoop

		KickFlash: // 14 tics
			PMSS A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MISS A 0
			"####" "DCBAAAAAABCDEF" 1
			Stop

		LeftKickFlash: // 11 tics
			PMSS A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MISS A 0
			"####" "AAAAAABCDEF" 1
			Stop

		RightKickFlash: // 15 tics
			PMSS A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MISS A 0
			"####" "AAAAAAAAAABCDEF" 1
			Stop

		KickInAirFlash: // 19 tics
			PMSS A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MISS A 0
			"####" "DCBAAAAAAAAAAABCDEF" 1
			Stop

		SlideKickFlashStart: // 39 tics
			PMSS A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MISS A 0
			"####" "DCB" 1
			"####" "A" 24
			Stop

		SlideKickFlashEnd: // 10 tics
			PMSS A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MISS A 0
			"####" "A" 5
			"####" "BCDEF" 1
			Stop

		GrenadeFlash: // 24 tics
			PMSS A 0 A_JumpIfInventory("IsPaladin", 1, 2)
			MISS A 0
			"####" "DCBA" 1
			Stop
	}
}

Actor Schism_RocketLauncherMag : Ammo {
   Inventory.Amount 0
   Inventory.MaxAmount 6
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 6
   Inventory.Icon "RLAUA0"
}